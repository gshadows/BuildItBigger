apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:2.0.0'
  }
}

android {
  compileSdkVersion 27
  buildToolsVersion "28.0.1"
  
  defaultConfig {
    applicationId "com.udacity.gradle.builditbigger"
    minSdkVersion 15
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"
    multiDexEnabled true
    
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  
  flavorDimensions "payment"
  
  productFlavors {
    free {
      dimension "payment"
      applicationId "com.udacity.gradle.builditbigger.free"
    }
    paid {
      dimension "payment"
      applicationId "com.udacity.gradle.builditbigger.paid"
    }
  }
  
}

dependencies {
  //implementation fileTree(dir: 'libs', include: ['*.jar'])
  
  implementation project(':jokesandroid')
  
  endpointsServer project(path: ':backend', configuration: 'endpoints')
  
  // Main dependencies.
  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation 'com.google.api-client:google-api-client:1.23.0'
  implementation 'com.google.http-client:google-http-client-android:1.23.0'
  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  
  // Free version: Google ads required.
  freeImplementation 'com.google.android.gms:play-services-ads:15.0.1'
  // Next dependencies fixes version conflicts with play-services-ads.
  freeImplementation 'com.android.support:support-v4:27.1.1'
  freeImplementation 'com.android.support:customtabs:27.1.1'
  freeImplementation 'com.android.support:support-media-compat:27.1.1'
  
  // Testing.
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'junit:junit:4.12'
  androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
  implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
}

task automatedFreeTest {//(type: Test) {
  description = "Start GCE server, run free tests and then shut GCE server down."
  group = "verification"
  
  dependsOn = [":backend:appengineStart", "connectedFreeDebugAndroidTest"]
  finalizedBy ":backend:appengineStop"
}
